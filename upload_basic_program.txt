1 'msx0babaput_decoder
5 CALL IOTPUT("msx/u0/pm/cpu/percent",200)
10 DEFINT A-Z:DIM F$, I$, O$, B$, T$, C$, B(4), OB(3):CLEAR 10000
20 LET F$ = "OUTPUT.BIN"
30 ON ERROR GOTO 2000:KILL F$
40 S=57:OPEN F$ AS #1 LEN=S
50 INPUT ":"; I$
60 IF I$="`" THEN CLOSE #1:CALL IOTPUT("msx/u0/pm/cpu/percent",100):END
70 GOSUB 100
80 GOSUB 1000
90 GOTO 50
100 'Decode
110 LET O$ = ""
120 LET B$ = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"
130 FOR I = 1 TO LEN(I$) STEP 4
140   T$ = MID$(I$, I, 4)
170   FOR J = 1 TO 4
180     B(J) = 0
190   NEXT J
200   FOR J = 1 TO 4
210     B(J) = INSTR(B$, MID$(T$, J, 1)) - 1:IF B(J) < 0 THEN B(J)=0
220   NEXT J
230   OB(1) = B(1) * 4 + B(2) \ 16
240   OB(2) = (B(2) AND 15) * 16 + B(3) \ 4
250   OB(3) = (B(3) AND 3) * 64 + B(4)
260   FOR J = 1 TO 3
270     O$ = O$ + CHR$(OB(J))
280   NEXT J
290 NEXT I
300 LET O$ = LEFT$(O$, LEN(O$) + (MID$(T$, 3, 1) = "=") + (MID$(T$, 4, 1) = "="))
310 RETURN
1000 'SAVE
1010 IF LEN(O$)<S THEN 1100
1020 FIELD #1,INT(S) AS T$
1030 LSET T$=O$:PUT #1
1040 RETURN
1100 CLOSE#1
1110 S=1:OPEN F$ AS #1 LEN=S
1120 F=LOF(1)
1130 FIELD #1,1 AS T$
1140 FOR I=1 TO LEN(O$)
1150   LSET T$=MID$(O$,I,1)
1160   PUT #1,F+I
1170 NEXT I
1180 RETURN
2000 IF ERL=30 AND ERR=53 THEN RESUME 40
2100 ON ERROR GOTO 0